buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.3.2'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.google.osdetector'

if (project.hasProperty("jfx-win")) {
    ext.platform = 'win'
    ext.platformSecified = true
} else if (project.hasProperty("jfx-mac")) {
    ext.platform = 'mac'
    ext.platformSecified = true
} else if (project.hasProperty("jfx-linux")) {
    ext.platform = 'linux'
    ext.platformSecified = true
} else {
    ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
    ext.platformSecified = false
}

group = 'com.skide'
version = '0.0.6'
mainClassName = 'com.skide.MainKt'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs 'static-libs'
    }
}

dependencies {
    compile "org.openjfx:javafx-base:12-ea+6:$platform"
    compile "org.openjfx:javafx-graphics:12-ea+6:$platform"
    compile "org.openjfx:javafx-controls:12-ea+6:$platform"
    compile "org.openjfx:javafx-fxml:12-ea+6:$platform"
    compile "org.openjfx:javafx-web:12-ea+6:$platform"
    compile "org.openjfx:javafx-media:12-ea+6:$platform"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.controlsfx:controlsfx:9.0.0'
    compile "org.json:json:20180813"
    compile name: 'terminalfx-1.3'
    compile name: 'analytics'
    compile 'com.jcraft:jsch:0.1.54'
    compile 'commons-net:commons-net:3.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

compileKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
}
compileTestKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
}
shadowJar {
    println 'Platform is: ' + platform
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    if (platformSecified)
        archiveName = "ide-${platform}.jar"
    else
        archiveName = "ide.jar"
}


jar {
    archiveName = "ide-core.jar"
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Name': name
        attributes 'Specification-Title': name
        attributes 'Specification-Version': version
        attributes 'Specification-Vendor': name
        attributes 'Implementation-Title': name
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor': name
    }
}
